#  MC6 - MC7 simulation
#  code obtained from Adam Watts
#  lengths are in mm, fields is Tesla !  Momentum in MeV
#______________________________________________________________________

param kill_aperture=0

physics QGSP_BIC

param worldMaterial=AIR
param inch=25.4
param ft=304.8
param quadrupoleColor=0.79,0.38,0.14
param dipoleColor=0,0,1
param trimColor=0.31,0.55,0.2
param vacuumColor=0.5,0.5,0.5
param collimatorColor=0.0,0.8,0.8
param scraperColor=0.8,0,0
param det_space=200.0
g4ui when=4 "/vis/viewer/set/background 1 1 1"
g4ui when=4 "vis/scene/add/axes 0 0 -1500 100000 mm"

param p=64.0 #selected momentum in GeV/c

# Dipole currents
param mc6di=452.5*($p/64.0)

param mc6q1i=19.31827941*($p/64.0)
param mc6q2i=-642.8273639*($p/64.0)
param mc6q3i=37.33991241*($p/64.0)
param mc6q4i=-29.57469877*($p/64.0)
param mc6q5i=429.9581841*($p/64.0)
param mc6q6i=-39.7818009*($p/64.0)

# Transfer functions [(T/m)/A]
param xfer_3Q120=0.18570791858
param xfer_4Q120=0.01842553191
param xfer_EPB=0.000888625592

# Quadrupole gradients [T/m]
param mc6Q1g=$mc6q1i*$xfer_3Q120
param mc6Q2g=$mc6q2i*$xfer_4Q120
param mc6Q3g=$mc6q3i*$xfer_3Q120
param mc6Q4g=$mc6q4i*$xfer_3Q120
param mc6Q5g=$mc6q5i*$xfer_4Q120
param mc6Q6g=$mc6q6i*$xfer_3Q120

# Dipole fields
param mc6db=$mc6di*$xfer_EPB

beam gaussian particle=proton nEvents=1.0E1 z=-200 sigmaX=1.0 sigmaY=1.0 y=-8 rotation=x-1.31 meanMomentum=120000.0
trackcuts keep=proton,mu+,pi+,pi0,kaon+

virtualdetector Det radius=10000.0 length=50 color=0,0,0

#------------Target&Cage,Absorber,Collimator----------------------
box Target  width=5 height=5 length=200 material=Cu color=1,0,0
place Target rename=MC6T1 z=100 rotation=x-1.31

box CageV height=450 width=100  length=2133.6 color=0.6,0.6,0.6  material=STAINLESS-STEEL kill=$kill_aperture
place CageV z=1016.5 x=-150 y=24.1 rotation=x-1.31
place CageV z=1016.5 x=+150 y=24.1 rotation=x-1.31

box CageH height=100  width=400 length=2133.6 color=0.6,0.6,0.6  material=STAINLESS-STEEL kill=$kill_aperture
place CageH z=1016.5 y=-251.9 rotation=x-1.31
place CageH z=1016.5 y=+299.17 rotation=x-1.31

box AbsV height=450 width=87.3 length=5.188*$ft color=1,0,0  material=CONCRETE kill=$kill_aperture
place AbsV z=1293.9 x=56.35 y=29.6 rotation=x-1.31
place AbsV z=1293.9 x=-56.35 y=29.6 rotation=x-1.31

box AbsH height=212.3 width=$inch length=5.1888*$ft color=1,0,0  material=CONCRETE kill=$kill_aperture
place AbsH z=1293.9 y=148.45 rotation=x-1.31
place AbsH z=1293.9 y=-89.25 rotation=x-1.31

box Coll_All height=31.75 width=431.8 length=1828.8 color=0,1,0 material=Al kill=$kill_aperture
box Coll_Concr height=914.4 width=914.4 length=1828.8 color=0,1,0 material=CONCRETE kill=$kill_aperture
place Coll_All z=3148 x=241.3 y=71.95 rotation=x-0.99
place Coll_All z=3148 x=-241.3 y=71.95 rotation=x-0.99
place Coll_Concr z=3148 y=545.096 rotation=x-0.99
place Coll_Concr z=3148 y=-401.196 rotation=x-0.99

#--------------------------------------------------------------------

genericbend EPB fieldHeight=35.6 fieldWidth=124.5 fieldLength=3048 fieldMaterial=AIR ironColor=$dipoleColor ironWidth=281.5 ironHeight=281.5 ironLength=3048 fringe=1 kill=$kill_aperture
genericquad 3Q120 fieldLength=3048 fieldMaterial=AIR ironLength=3048 fringe=1 apertureRadius=38.1 ironRadius=305 ironColor=$quadrupoleColor kill=$kill_aperture
genericquad 4Q120 fieldLength=3048 fieldMaterial=AIR ironLength=3048 fringe=1 apertureRadius=50.8 ironRadius=438  ironColor=$quadrupoleColor kill=$kill_aperture
genericbend 4430 fieldHeight=101.6 fieldWidth=101.6 fieldLength=762 fieldMaterial=AIR ironLength=762 ironHeight=295 ironWidth=397 ironColor=$trimColor fringe=1 kill=$kill_aperture
genericbend Mom_Coll fieldHeight=127 fieldWidth=127 fieldLength=1676.4 fieldMaterial=AIR ironLength=1676.4 ironHeight=295 ironWidth=397 ironColor=$collimatorColor By=0 fringe=0 kill=$kill_aperture

genericquad Vac gradient=0 fringe=0 apertureRadius=120 ironRadius=121 fieldMaterial=Vacuum ironColor=0.5,0.5,0.5
place Vac fieldLength=141.29 ironLength=141.29 z=4908.69  y=112.66 rotation=x-1.31
place Vac fieldLength=404.89 ironLength=404.89 z=8158.135  y=189.461 rotation=x-1.31 
place Vac fieldLength=428.634 ironLength=428.634 z=11621.8995  y=269.0173 rotation=x-1.22
place Vac fieldLength=305.186 ironLength=305.186 z=15035.807  y=335.47 rotation=x-1.1
place Vac fieldLength=305.5 ironLength=305.5 z=18388.15  y=393.051 rotation=x-0.95

place Det rename=Det1 z=6432.19-3048/2-$det_space y=147.5 rotation=x-1.31 format=ascii file=1.txt
place 3Q120 rename=Q1_1 z=6432.19 y=147.5 rotation=x-1.31 gradient=$mc6Q1g fieldMaterial=Vacuum

place Det rename=Det2 z=9884.08-3048/2-$det_space y=224.3 rotation=x-1.31 format=ascii file=2.txt
place 3Q120 rename=Q1_2 z=9884.08 y=224.3 rotation=x-1.31 gradient=$mc6Q1g fieldMaterial=Vacuum

place Det rename=Det3 z=13359.7144-3048/2-$det_space y=299.6 rotation=x-1.15 format=ascii file=3.txt
place EPB rename=MC6D1 z=13359.7144 y=299.6 By=$mc6db rotation=z90,x-1.15 fieldMaterial=Vacuum

place Det rename=Det4 z=16711.9-3048/2-$det_space y=361.8 rotation=x-0.99 format=ascii file=4.txt
place 4Q120 rename=Q2 z=16711.9 y=361.8 rotation=x-0.99 gradient=$mc6Q2g fieldMaterial=Vacuum

place Det rename=Det5 z=20063.4-3048/2-$det_space y=414.8328 rotation=x-0.8194 format=ascii file=5.txt
place EPB rename=MC6D2 z=20064.4 y=414.8328 By=$mc6db rotation=z90,x-0.8194 fieldMaterial=Vacuum
place Det rename=Det6 z=20063.4+3048/2+$det_space y=414.8328 rotation=x-0.65 format=ascii file=6.txt

group Scraper1
  box Scrap_Steel_Side height=50.799 width=441.299 length=997 color=$scraperColor material=STAINLESS-STEEL kill=$kill_aperture
  box Scrap_Steel_Bottom height=25.399 width=914.4 length=997 color=$scraperColor material=STAINLESS-STEEL kill=$kill_aperture
  box Scrap_Steel_Top height=203.199 width=914.4 length=997 color=$scraperColor material=STAINLESS-STEEL kill=$kill_aperture
  box Scrap_ConcrUp height=914.4 width=914.4 length=997 color=$scraperColor material=CONCRETE kill=$kill_aperture
  box Scrap_ConcrDown height=1828.8 width=914.4 length=997 color=$scraperColor material=CONCRETE kill=$kill_aperture
  place Scrap_Steel_Side rename=+_steel_left x=236.5499 z=498.5
  place Scrap_Steel_Side rename=+_steel_right x=-236.5499 z=498.5
  place Scrap_Steel_Top  rename=+=steel_top y=127 z=498.5
  place Scrap_Steel_Bottom  rename=+=steel_bottom y=-38.1 z=498.5
  place Scrap_ConcrUp rename=+_concrete_up y=685.8 z=498.5
  place Scrap_ConcrDown rename=+_concrete_down y=-965.2 z=498.5
endgroup
place Scraper1 rename=Scraper1 y=445.9224 z=22393.072 rotation=x-0.65
#----------------

place Det rename=Det7 z=24721.744-3048/2-$det_space y=472.745 rotation=x-0.653 format=ascii file=7.txt
place 3Q120 rename=Q3 z=24721.744 y=472.745 rotation=x-0.653 gradient=$mc6Q3g

place Det rename=Det8 z=26931.24-762/2-$det_space y=499.871 rotation=x-0.917 format=ascii file=8.txt
place 4430 rename=VT1 z=26931.24 y=499.871 By=0 rotation=z90,x-0.917
place Det rename=Det9 z=27998.04-762/2-$det_space y=515.417 rotation=x-0.917 format=ascii file=9.txt
place 4430 rename=HT1 z=27998.04 y=515.417 rotation=x-0.917 By=0

#---Scraper2----
group Scraper2
  box Scrap2_Steel height=101.6 width=457.2 length=1003.4 color=$scraperColor material=STAINLESS-STEEL kill=$kill_aperture
  box Scrap2_ConcrUp height=914.4 width=914.4 length=1003.4 color=$scraperColor material=CONCRETE kill=$kill_aperture
  box Scrap2_ConcrDown height=1828.8 width=914.4 length=1003.4 color=$scraperColor material=CONCRETE kill=$kill_aperture
  place Scrap2_Steel rename=+_steel_left x=279.4 z=501.7
  place Scrap2_Steel rename=+_steel_right x=-279.4 z=501.7
  place Scrap2_ConcrUp rename=+_concrete_up y=508. z=501.7
  place Scrap2_ConcrDown rename=+_concrete_down y=-965.2 z=501.7
endgroup
place Det rename=Det10 z=29236.4166-700 y=529.6234 rotation=x-0.66 format=ascii file=10.txt
place Scraper2 rename=Scraper2 y=529.6234 z=29236.4166 rotation=x-0.66
#----------------

place Det rename=Det11 z=33049.7944-1676.4/2-$det_space y=568.15 rotation=x-0.656 format=ascii file=11.txt
place Mom_Coll z=33049.7944 y=568.15 rotation=x-0.656
place Det rename=Det12 z=33049.7944+1676.4/2+$det_space y=568.15 rotation=x-0.66 format=ascii file=12.txt

#---Scraper3----
group Scraper3
  box Scrap3_Steel height=50.8 width=457.2 length=1003.4 color=$scraperColor material=STAINLESS-STEEL kill=$kill_aperture
  box Scrap3_ConcrUp height=914.4 width=914.4 length=1003.4 color=$scraperColor material=CONCRETE kill=$kill_aperture
  box Scrap3_ConcrDown height=1828.8 width=914.4 length=1003.4 color=$scraperColor material=CONCRETE kill=$kill_aperture
  place Scrap3_Steel rename=+_steel_left x=254 z=501.7
  place Scrap3_Steel rename=+_steel_right x=-254 z=501.7
  place Scrap3_ConcrUp rename=+_concrete_up y=482.6 z=501.7
  place Scrap3_ConcrDown rename=+_concrete_down y=-939.8 z=501.7
endgroup
place Scraper3 rename=Scraper3 y=568.15 z=33048.7944 rotation=x-0.66
#----------------

place Det rename=Det13 z=36831.7784-762/2-$det_space y=610.2096 rotation=x-0.917 format=ascii file=13.txt
place 4430 rename=VT2 z=36831.7784 y=610.2096 By=0 rotation=z90,x-0.664 kill=$kill_aperture
place Det rename=Det14 z=37798.5784-700/2-$det_space y=622.7064 rotation=x-0.917 format=ascii file=14.txt
place 4430 rename=HT2 z=37798.5784 y=622.7064 rotation=x-0.665 By=0 kill=$kill_aperture
place Det rename=Det15 z=40008.38-3048/2-$det_space y=648.0048 rotation=x-0.917 format=ascii file=15.txt
place 3Q120 rename=Q4 z=40008.38 y=648.0048 rotation=x-0.659 gradient=$mc6Q4g
place Det rename=Det16 z=43360.88-3048/2-$det_space y=681.8376 rotation=x-0.917 format=ascii file=16.txt
place EPB rename=MC6D3 z=43360.88 y=681.8376 By=$mc6db rotation=z90,x-0.4927
place Det rename=Det17 z=46713.67-3048/2-$det_space y=705.612 rotation=x-0.917 format=ascii file=17.txt
place 4Q120 rename=Q5 z=46713.67 y=705.612 rotation=x-0.327 gradient=$mc6Q5g
place Det rename=Det18 z=50066.48-3048/2-$det_space y=720.5472 rotation=x-0.917 format=ascii file=18.txt
place EPB rename=MC6D4 z=50066.48 y=720.5472 By=$mc6db rotation=z90,x-0.166
place Det rename=Det19 z=53419.28-3048/2-$det_space y=724.8144 rotation=x-0.917 format=ascii file=19.txt
place 3Q120 rename=Q6 z=53419.28 y=724.8144 gradient=$mc6Q6g

box CollV height=4572 width=1800 length=1800 color=0,1,0  material=CONCRETE kill=$kill_aperture
place CollV z=58000 x=1204.8 y=724.8
place CollV z=58000 x=-1204.8 y=724.8

box CollH height=1800 width=609.6 length=1800 color=0,1,0  material=CONCRETE kill=$kill_aperture
place CollH z=58000 y=1929.6
place CollH z=58000 y=-480

genericquad HeTube gradient=0 fringe=0 apertureRadius=202 ironRadius=203 fieldMaterial=He ironColor=102,204,255
param He_length=49000.0
place HeTube fieldLength=$He_length ironLength=$He_length z=59000+0.5*$He_length y=724.8144

#----------------------MC7-----------------------------------------
place Det rename=NovaTarget z=108254 y=724.8144 format=ascii file=20.txt #NOvA target
place Det rename=NOvAdet z=108254+14.617E3 y=724.8144 format=ascii file=21.txt #NOvA first detector block
